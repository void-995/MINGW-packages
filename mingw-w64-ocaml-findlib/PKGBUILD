# Maintainer: Junjie Mao <eternal.n08@gmail.com>

_realname=ocaml-findlib
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=1.5.6
pkgrel=1
pkgdesc="OCaml library manager (mingw-w64)"
arch=('any')
url='http://projects.camlcity.org/projects/findlib.html'
license=('custom')
depends=("${MINGW_PACKAGE_PREFIX}-ocaml")
source=("http://download.camlcity.org/download/findlib-${pkgver}.tar.gz")
sha256sums=('7743b3dcaf8a344c882e89f61601f7f086d46c0f2ea4fcc3e54069236825e3f8')

prepare() {
  plain "skip"
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  cp -rf findlib-${pkgver} build-${MINGW_CHOST}

  unset FLEXDIR
  unset OCAMLDIR

  cd ${srcdir}/build-${MINGW_CHOST}
  PATH=${MINGW_PREFIX}/lib/ocaml/stublibs:$PATH \
  ./configure -system win32 \
              -sitelib ${MINGW_PREFIX}/lib/ocaml \
              -mandir ${MINGW_PREFIX}/share/man \
              -no-camlp4

  make all -j1
  make opt -j1
}

check() {
  # The package does not include a testsuite
  plain "skip"
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make install prefix=${pkgdir}
  [ -f LICENSE ] && install -Dm644 LICENSE ${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE
}
